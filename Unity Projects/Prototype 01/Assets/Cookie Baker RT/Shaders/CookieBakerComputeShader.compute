// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

struct ObjectDatum
{
	float4x4	LocalToWorldMatrix;
	int			IndicesOffset;
	int			IndicesCount;
};

static const float EPSILON = 1e-8;

int								_MaxSegments;
float4							_LightPosition;
float4							_LightForwardDir;
float4							_LightUpwardDir;
float							_InnerRange;
float							_OuterRange;
float							_SpotLightAngle;
float							_ShadowFocusDistance;
StructuredBuffer<ObjectDatum>	_ObjectData;
StructuredBuffer<float3>		_Vertices;
StructuredBuffer<int>			_Indices;

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
RWTexture2D<float4> Result;


struct LightRay
{
	float3		Direction;
	float3		Color;
	float3		Position;
};

struct RayHit
{
	float3		Position;
	float		Distance;
	float3		Normal;
};

// Creates our inital light rays.
// uv: The uv position that the ray hits in the final texture result.
// halfSize: If we were to convert our resulting cookie texture to World-Space, this would be half its width (or height) at the given focus distance.
// lightRightwardDir: This is the light-source's Right Direction in World-Space. This value wasn't given to the shader, so it had to be computed.
LightRay CreateInitialLightRay(float2 uv, float halfSize, float4 lightRightwardDir)
{
	LightRay result;

	result.Color = float3(1.0, 1.0, 1.0);	// We will start with a pure white light, if the User wants to tint it, they can tint the actual light.
	result.Position = _LightPosition.xyz;	// Our starting position is the position of our light-source.

	// If we were to place our shadow texture in World-Space at the distance given by _ShadowFocusDistance, 
	// we want the offset vector that goes from our lightSource to our uv position. In other words, if we 
	// add this vector to our position, we will get a World-Space position that matches up to the given 
	// uv-position at the _ShadowFocusDistance.
	float3 hitPosOffset = (uv.x * halfSize) * lightRightwardDir.xyz;
	hitPosOffset += (uv.y * halfSize) * _LightUpwardDir.xyz;
	hitPosOffset += _ShadowFocusDistance * _LightForwardDir.xyz;

	// Normalize the offset we calculated into a direction vector.
	result.Direction = normalize(hitPosOffset);

	return result;
}

RayHit CreateRayHit()
{
	RayHit hit;

	hit.Position = float3(0.0f, 0.0f, 0.0f);
	hit.Distance = 1.#INF;
	hit.Normal = float3(0.0f, 0.0f, 0.0f);

	return hit;
}

// https://www.scratchapixel.com/lessons/3d-basic-rendering/ray-tracing-rendering-a-triangle/barycentric-coordinates
bool RayTriangleIntersect(LightRay ray, float3 v0, float3 v1, float3 v2, 
						  inout float t, inout float u, inout float v, 
						  inout float3 P, inout float3 N)
{
	// Compute plane's normal
	float3 v0v1 = v1 - v0;
	float3 v0v2 = v2 - v0;

	// No need to normalize
	N = cross(v0v1, v0v2);
	float denom = dot(N, N);

	///
	/// Step 1: Finding P
	///

	// Check if ray and plane are parallel?
	float NdotRayDirection = dot(N, ray.Direction);
	if (abs(NdotRayDirection) < EPSILON)
		return false;	// They are parallel so they don't intersect

	// Compute d parameter using equation 2
	float d = dot(N, v0);

	// Compute t (equation 3)
	t = (dot(N, ray.Position) + d) / NdotRayDirection;

	// Check if the triangle is behind the ray.
	if (t < 0)
		return false;	// The triangle is behind the ray

	// Compute the intersection point using equation 1.
	P = ray.Position + (t * ray.Direction);


	///
	/// Step 2: Inside-outside test
	///

	float3 C;	// Vector perpendicular to triangle's plane

	// Edge 0
	float3 edge0 = v1 - v0;
	float3 vp0 = P - v0;
	C = cross(edge0, vp0);
	if (dot(N, C) < 0)
		return false;	// P is on the right side
						// Note from FCT: P needs to be on the left-side of the edge (vector v0v1) in order to be inside the triangle.

	// Edge 1
	float3 edge1 = v2 - v1;
	float3 vp1 = P - v1;
	C = cross(edge1, vp1);
	u = dot(N, C);
	if (u < 0)
		return false;	// P is on the right side.

	// Edge 2
	float3 edge2 = v0 - v2;
	float3 vp2 = P - v2;
	C = cross(edge2, vp2);
	v = dot(N, C);
	if (v < 0)
		return false;	// P is on the right side.

	u /= denom;
	v /= denom;
	N = normalize(N);

	return true; // This ray hits the triangle.
}


void IntersectMeshObject(LightRay lightRay, inout RayHit bestHit, ObjectDatum meshObject)
{
	uint offset = meshObject.IndicesOffset;
	uint count	= offset + meshObject.IndicesCount;

	for (uint i = offset; i < count; i += 3)
	{
		float3 v0 = (mul(meshObject.LocalToWorldMatrix, float4(_Vertices[_Indices[i + 0]], 1.0f))).xyz;
		float3 v1 = (mul(meshObject.LocalToWorldMatrix, float4(_Vertices[_Indices[i + 1]], 1.0f))).xyz;
		float3 v2 = (mul(meshObject.LocalToWorldMatrix, float4(_Vertices[_Indices[i + 2]], 1.0f))).xyz;

		float t, u, v;
		float3 P;
		float3 N;
		bool theresAHit = RayTriangleIntersect(lightRay, v0, v1, v2, t, u, v, P, N);

		if (theresAHit)
		{
			if (t < bestHit.Distance)
			{
				bestHit.Distance	= t;
				bestHit.Position	= P;
				bestHit.Normal		= N;
			}
		}
	}
}

RayHit Trace(LightRay ray)
{
	RayHit bestHit = CreateRayHit();
	uint count, stride, i;

	_ObjectData.GetDimensions(count, stride);
	for (i = 0; i < count; i++)
	{
		IntersectMeshObject(ray, bestHit, _ObjectData[i]);
	}

	return bestHit;
}


[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
	uint width;
	uint height;

	Result.GetDimensions(width, height);

	float pixX = id.x + 0.5f;
	float pixY = id.y + 0.5f;

	float2 uv = float2(pixX / width, pixY / height);
	uv *= 2.0;
	uv -= 1.0;

	bool outOfBounds = dot(uv, uv) > 1.0;

	if (outOfBounds)
	{
		Result[id.xy] = float4(0.0, 0.0, 0.0, 0.0);
		return;
	}

	float4	lightRightwardDir	= float4(cross(_LightUpwardDir.xyz, _LightForwardDir.xyz), 0.0);
	float	halfSize			= _ShadowFocusDistance * tan(_SpotLightAngle);

	///
	/// Create inital LightRay for our Ray-Tracing.
	///
	LightRay lightRay = CreateInitialLightRay(uv, halfSize, lightRightwardDir);

	
	///
	/// Ray tracing code
	///
	for (int i = 0; i < _MaxSegments; i++)
	{
		RayHit hit = Trace(lightRay);
		if (hit.Distance < (2.0 * _OuterRange))
		{
			lightRay.Color *= 0.75f;
			lightRay.Direction = hit.Normal;
			lightRay.Position = hit.Position + (0.0005f * hit.Normal);
		}
		else
		{
			i = _MaxSegments;
		}
	}


	// If the light is pointing away from cookie-texture, then it can not add to the texture.
	if (dot(lightRay.Direction, _LightForwardDir.xyz) <= 0.0)
		return;

	/// 
	/// Convert our lightRay into a point on the plane that our Cookie will cover in World-Space at a 
	/// distance of _ShadowFocusDistance from our light-source
	///

	// This is uv-point (0, 0) in our plane.
	float3 v0			= (_ShadowFocusDistance * _LightForwardDir + _LightPosition).xyz;

	// This is the LightRay's current starting point.
	float3 p0			= lightRay.Position;


	// This is the delta vector from our LightRay's starting point to plane's 0-point.
	float3 W			= p0 - v0;
	
	// Normal vector for our plain
	float3 N			= (-_LightForwardDir).xyz;


	// This is the scale factor by which we multiple our lightRay's direction in order to find the offset 
	// for the intersection point
	float s_i			= dot(-N, W) / dot(N, lightRay.Direction);

	// This is our intersection point with the plane.
	float3 lightPoint	= (s_i * lightRay.Direction) + p0;

	///
	/// Convert our lightPoint in World-Space into a uv-coord.
	///
	float3	planeCoord = lightPoint - v0;
	float	uOffset = dot(planeCoord, lightRightwardDir.xyz);
	float	vOffset = dot(planeCoord, _LightUpwardDir.xyz);
	float2	uvPrime = float2(uOffset, vOffset);
	uvPrime *= (1.0 / halfSize);

	// Check to see if we're even in the allowed texture area.
	if (dot(uvPrime, uvPrime) > 1.0)
		return;

	float2 pix = uvPrime + 1.0;
	pix *= (1.0 / 2.0);
	pix *= width;

	int2 pixInt = pix;

	Result[pixInt.xy] += float4(1.0, 1.0, 1.0, 1.0);
}
